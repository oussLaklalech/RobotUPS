import Nest.Interface.IConfigureEcoNest
import Nest.Interface.ICreateNest
import Nest.Interface.IGettersNest
import Nest.Interface.INestsInfo
import Robot.interfaces.IBrain
import Robot.interfaces.IConfigureEcoRobots
import Robot.interfaces.ICreateRobot
import Robot.interfaces.IEcoRobotInfo
import Robot.interfaces.IEye
import Robot.interfaces.IFootHand
import Robot.interfaces.IGettersRobot
import Robot.interfaces.ILife
import Robot.interfaces.IPlay
import box.Interface.IBoxesInfo
import box.Interface.IConfigureEcoBox
import box.Interface.ICreateBox
import box.Interface.IGettersBox
import box.Interface.IManageBoxes
import datatype.Position
import gui.Interface.IGetInfo
import gui.Interface.IManageCell
import gui.Interface.IManageGui
import java.awt.Color
import logging.Interface.ILogger

namespace Robot {
	ecosystem EcoNest {
		provides create: ICreateNest
		provides informationAboutNestsGive : INestsInfo
		provides setConfiguration : IConfigureEcoNest
	
		requires nestManageGui : IManageGui
		species Nest(color: Color, position: Position){
			provides getInfoNest: IGettersNest
		}
	}
	
	ecosystem EcoRobots {
		provides create: ICreateRobot
		provides setConfiguration : IConfigureEcoRobots
		provides informationAboutRobot :IEcoRobotInfo
		requires robotManageGui : IManageGui
		requires informationAboutBoxesNeed : IBoxesInfo
		requires informationAboutNestsNeed : INestsInfo
		requires manageBoxesNeed : IManageBoxes
		provides log : ILogger	
	
		species Robot(color: Color, position: Position) {
			provides percevoir: IEye
			provides decider: IBrain
			provides agir: IFootHand
		   
		    provides play: IPlay
		    provides seSuicider: ILife
		    provides getInfoRobot: IGettersRobot
		}
	}
	
	ecosystem EcoBoxes {
		provides create: ICreateBox
		provides informationAboutBoxesGive : IBoxesInfo
		provides setConfiguration : IConfigureEcoBox
		provides manageBoxesGive : IManageBoxes
		
		requires boxManageGui : IManageGui
		
		species Box(color: Color, position: Position) {
			provides getInfoBox: IGettersBox	
		}
	}
	
	ecosystem GridGui {
			provides manageGui :IManageGui
			requires configureSystemeRobots : IConfigureEcoRobots
			requires configureSystemeNests : IConfigureEcoNest
			requires configureSystemeBoxs : IConfigureEcoBox
			requires createRobots : ICreateRobot
			requires createBox : ICreateBox
		    requires infoBox : IBoxesInfo
		    requires infoNest : INestsInfo
		    requires infoRobot :IEcoRobotInfo
		species CellGui(position : Position) {
			provides manageCell :IManageCell
			provides getInfo : IGetInfo
		}	
	}
	
	ecosystem Environnement {
				
		part nests : EcoNest
		{
			bind nestManageGui to gridGui.manageGui
		}
		
		part boxes : EcoBoxes
		{
			bind boxManageGui to gridGui.manageGui
		}
		
		part gridGui : GridGui{
			bind  createBox to boxes.create
			bind createRobots to robots.create
			bind configureSystemeRobots to robots.setConfiguration
			bind configureSystemeNests to nests.setConfiguration
			bind configureSystemeBoxs to boxes.setConfiguration
			bind infoBox to boxes.informationAboutBoxesGive
			bind infoNest to nests.informationAboutNestsGive
			bind infoRobot to robots.informationAboutRobot
			
		}
	
		part robots : EcoRobots{
			bind informationAboutNestsNeed to nests.informationAboutNestsGive
			bind informationAboutBoxesNeed to boxes.informationAboutBoxesGive
			bind robotManageGui to gridGui.manageGui
	
			bind manageBoxesNeed to boxes.manageBoxesGive
		}
		
		
	}
	
	
}
